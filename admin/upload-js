#!/usr/bin/perl
use lib "../lib";
use blopcgi js => 1;

$blop->read_conf();

if (!$blop->admin) {
    die "Admin privilege required!\n";
}

my $postid = $cgi->param("postid");
my $post;
if ($postid) {
    $post = $blop->post(postid => $postid) or die "Invalid postid.\n";
}
else {
    my $sth = $blop->dbh->prepare("insert into posts set added=now()");
    $sth->execute();
    $postid = $sth->{mysql_insertid};
}

my $uploadfh = $cgi->upload("file") or die "Couldn't find upload filehandle.\n";
my $uploadfh2 = $uploadfh->handle;

my $dir = "$blop->{base}content/post/$postid";
if (!-e $dir) {
    mkdir $dir or die "Unable to mkdir $dir: $!\n";
}

my $upload = $cgi->param("file") or die "No file name given.\n";
if ($upload =~ m{(^|/)\.\.($|/)}) {
    die "File name can't include \"..\".\n";
}
if ($upload =~ m{/}) {
    die "File name can't include \"/\".\n";
}

my $path = "$dir/$upload";
if ($upload eq "image.jpg" && -e $path) {
    my $i = 2;
    while (1) {
        $upload = "image$i.jpg";
        $path = "$dir/$upload";
        last if !-e $path;
        $i++;
    }
}

open my $fh, ">", $path or die "Unable to open $path: $!\n";
binmode $fh;
my $buffer;
while ($uploadfh2->read($buffer, 1024)) {
    print $fh $buffer;
}
close $fh;

my $url = "/content/post/$postid/$upload";
my $file = {
    name => $upload,
    path => $path,
    url => $url,
    fullurl => "$blop->{urlbase}$url",
    size => $blop->human_readable(-s $path),
    postid => $postid,
};

if ($post) {
    $post->update_content(file => $file);
}

print "Content-Type: application/json\n\n";
print $blop->escape_json($file);

