#!/usr/bin/perl
use lib "../lib";
use blopcgi json => 1;

$blop->read_conf;
$blop->assert_admin;
$blop->assert_csrf;

my $now = $blop->dbh->quote($blop->now->str);

my $postid = $cgi->param("postid");
my $pageid = $cgi->param("pageid");
my $section = $cgi->param("section");
my $entry;
if ($section) {
    $entry = $blop->section($section) or die "Invalid section.\n";
}
elsif ($postid) {
    $entry = $blop->post(postid => $postid) or die "Invalid postid.\n";
}
elsif ($pageid) {
    $entry = $blop->page(pageid => $pageid) or die "Invalid pageid.\n";
}
elsif (defined $postid) {
    my $sth = $blop->dbh->prepare("insert into posts set added=$now");
    $sth->execute();
    $postid = $sth->{mysql_insertid};
    $entry = bless {postid => $postid}, "Blop::Post";
    $blop->log(content => "Added post \"Post $postid\" stub for uploads", postid => $postid);
}
elsif (defined $pageid) {
    my $sth = $blop->dbh->prepare("insert into pages set added=$now");
    $sth->execute();
    $pageid = $sth->{mysql_insertid};
    $entry = bless {pageid => $pageid}, "Blop::Post";
    $blop->log(content => "Added page \"Page $pageid\" stub for uploads", pageid => $pageid);
}
else {
    die "An entry is required.\n";
}

my $uploadfh = $cgi->upload("file") or die "Couldn't find upload filehandle.\n";
my $uploadfh2 = $uploadfh->handle;

my $dir = $entry->content_path;
if (!-e $dir) {
    mkdir $dir or die "Unable to mkdir $dir: $!\n";
}
if (!-e "$dir/files") {
    mkdir "$dir/files" or die "Unable to mkdir $dir/files: $!\n";
}

my $name = $cgi->param("file") or die "No file name given.\n";
if ($name =~ m{(^|/)\.\.($|/)}) {
    die "File name can't include \"..\".\n";
}
if ($name =~ m{/}) {
    die "File name can't include \"/\".\n";
}

my $path = "$dir/files/$name";

# iPhone names all images image.jpg, so don't overwrite each one
if ($name eq "image.jpg" && -e $path) {
    my $i = 2;
    while (1) {
        $name = "image$i.jpg";
        $path = "$dir/files/$name";
        last if !-e $path;
        $i++;
    }
}

open my $fh, ">", $path or die "Unable to open $path: $!\n";
binmode $fh;
my $buffer;
while ($uploadfh2->read($buffer, 1024)) {
    print $fh $buffer;
}
close $fh;

my $url = "/" . $entry->content_url . "/files/$name";
my $file = {
    name => $name,
    path => $path,
    url => $url,
    fullurl => "$blop->{urlbase}$url",
    size => $blop->human_readable(-s $path),
};
$file->{pageid} = $pageid if $pageid;
$file->{postid} = $postid if $postid;

$entry->update_content(file => $name);

print $blop->http_header("Content-Type" => "application/json");
print $blop->escape_json($file);

