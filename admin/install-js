#!/usr/bin/perl
use lib "../lib";
use blopcgi js => 1;
use DBI;

if ($cgi->param("timezone")) {
    $ENV{TZ} = $cgi->param("timezone");
}

my $out = {};

if (!$cgi->param("title")) {
    $out->{titleError} = "Please enter a title.";
    $out->{error} = 1;
}

if ($cgi->param("email") && $cgi->param("email") !~ /\S+\@\S+\.\S+/) {
    $out->{emailError} = "This email doesn't look right.";
    $out->{error} = 1;
}

my $pass = $cgi->param("pass") || "";
if (!$pass) {
    $out->{passError} = "Please enter a password.";
    $out->{error} = 1;
}

my $confirm = $cgi->param("confirm") || "";
if ($confirm ne $pass) {
    $out->{confirmError} = "Password confirmation didn't match.";
    $out->{error} = 1;
}

my $dbhost = $cgi->param("dbhost") || "";
my $dbuser = $cgi->param("dbuser") || "";
my $dbpass = $cgi->param("dbpass") || "";
my $dbtable = $cgi->param("dbtable") || "";
my $dbh = DBI->connect(
    "dbi:mysql:host=$dbhost;database=$dbtable;mysql_multi_statements=1",
    $dbuser, $dbpass, {PrintError => 0, RaiseError => 0});
if (!$dbh) {
    $out->{dbhostError} = $DBI::errstr;
    $out->{dbuserError} = "";
    $out->{dbpassError} = "";
    $out->{dbtableError} = "";
    $out->{error} = 1;
}

if ($out->{error}) {
    print $blop->http_header("Content-Type" => "application/json");
    print $blop->escape_json($out);
    exit;
}

my %conf;
$conf{dbuser} = $cgi->param("dbuser");
$conf{dbpass} = $cgi->param("dbpass");
$conf{dbhost} = $cgi->param("dbhost");
$conf{dbtable} = $cgi->param("dbtable");
Blop::Config::write("$blop->{base}blop.conf", \%conf);

$dbh->{RaiseError} = 1;

$dbh->do(<<EOSQL);
create table posts (
    postid int unsigned primary key auto_increment,
    title varchar(128) not null,
    url varchar(128) unique,
    added datetime,
    published datetime,
    categoryid int unsigned not null,
    content text not null);

create table categories (
    categoryid int unsigned primary key auto_increment,
    name varchar(128) not null unique,
    url varchar(128) unique,
    special varchar(128) unique);

create table tags (
    tagid int unsigned primary key auto_increment,
    postid int unsigned not null,
    name varchar(128) not null,
    unique (postid, name));

create table pages (
    pageid int unsigned primary key auto_increment,
    title varchar(128),
    url varchar(128) unique,
    added datetime,
    published datetime,
    content text,
    sequence float not null,
    parentid int unsigned not null);

create table comments (
    commentid int unsigned primary key auto_increment,
    postid int unsigned,
    pageid int unsigned,
    name varchar(128),
    email varchar(128),
    ipaddr varchar(16),
    cookie varchar(32),
    added datetime,
    edited datetime,
    status enum("Pending", "Approved", "Spam"),
    content text);

create table sessions (
    sessionid varchar(32) not null primary key,
    admin int unsigned,
    ipaddr varchar(16),
    added datetime,
    csrf varchar(32) not null);

create table config (
    name varchar(64) not null primary key,
    value text);

create table log (
    logid int unsigned primary key auto_increment,
    date datetime not null,
    ipaddr varchar(16),
    uri varchar(128),
    content text,
    postid int unsigned,
    pageid int unsigned,
    categoryid int unsigned);
EOSQL

my $sth = $dbh->prepare("insert into config set name=?, value=?");
$sth->execute(title => $cgi->param("title"));
$sth->execute(slogan => $cgi->param("slogan"));
$sth->execute(email => $cgi->param("email"));
$sth->execute(pass => $cgi->param("pass"));
$sth->execute(timezone => $cgi->param("timezone"));
$sth->execute(ppp => 4);
$sth->execute(post_comments => 1);
$sth->execute(page_comments => 1);
$sth->execute(mod_comments => 1);
$sth->execute(theme => "default");
$sth->execute(gallery_small => "x125>");
$sth->execute(gallery_medium => "x250>");
$sth->execute(gallery_large => "700>");
$sth->execute(sidebar => "[search /]\n[categories /]\n[archives /]\n[admin-links /]\n");

$sth = $dbh->prepare("insert into categories set special=?, name=?, url=?");
$sth->execute(allcat => "All", "");
$sth->execute(uncat => "Uncategorized", "uncategorized");

$blop->{dbh} = $dbh;
$blop->log(content => "Installed");

$blop->create_session();

print $blop->http_header("Content-Type" => "application/json");
print $blop->escape_json({ok => 1});

