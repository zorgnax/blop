#!/usr/bin/perl
use lib "../lib";
use blopcgi js => 1;

$blop->read_conf;
$blop->assert_admin;
$blop->assert_csrf;

my $postid = $cgi->param("postid");
my $pageid = $cgi->param("pageid");
my $section = $cgi->param("section");
my $entry;
if ($section) {
    $entry = $blop->section($section) or die "Invalid section.\n";
}
elsif ($postid) {
    if ($postid !~ /^\d+$/) {
        die "Invalid postid.\n";
    }
    $entry = bless {postid => $postid}, "Blop::Post";
}
elsif ($pageid) {
    if ($pageid !~ /^\d+$/) {
        die "Invalid pageid.\n";
    }
    $entry = bless {pageid => $pageid}, "Blop::Page";
}
else {
    die "An entry is required.\n";
}

my $name = $cgi->param("name");
if (!$name) {
    die "A file name is required.\n";
}
if ($name =~ m{(^|/)\.\.($|/)}) {
    die "Name cannot traverse upwards.\n";
}

my $path = $entry->content_path . "/$name";
unlink $path;

for my $path (glob $entry->content_path . "/thumb/*") {
    $path =~ m{([^/]+)\.[^\.]+\.(\w+)$} or next;
    if ("$1.$2" eq $name) {
        unlink $path;
    }
}

print $blop->http_header("Content-Type" => "application/json");
print $blop->escape_json({
    ok => 1,
    mesg => "Deleted $name\n",
});

