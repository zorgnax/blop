#!/usr/bin/perl
use strict;
use warnings;
use lib "lib";
use Blop;
use Blop::Markup;
use Data::Dumper;

my $content_path;
my $dump;

my @args = @ARGV;
@ARGV = ();
while (my $arg = shift @args) {
    if ($arg eq "--") {
        push @ARGV, @args;
        last;
    }
    elsif ($arg eq "-c") {
        $content_path = shift @args;
    }
    elsif ($arg eq "-d") {
        $dump = 1;
    }
    elsif ($arg =~ /^(--?help|-h|-\?)$/) {
        usage();
    }
    elsif ($arg =~ /^-/) {
        die "Invalid argument '$arg'\n";
    }
    else {
        push @ARGV, $arg;
    }
}

my $blop = Blop->new(urlbase => ".");
$Blop::BBCode::bbcode{example} = {
    block => 1, norecurse => 1, display => \&display_example
};

my $post;
if ($content_path) {
    $post = bless {content_url => $content_path}, "Blop::Post";
}
my $markup = Blop::Markup->new(entry => $post, update => 1);

for my $file (@ARGV) {
    open my $fh, "<", $file or die "Can't open $file: $!\n";
    my $input = do {local $/; <$fh>};
    close $fh;
    convert($input);
}
if (!@ARGV) {
    my $input = do {local $/; <STDIN>};
    convert($input);
}

sub convert {
    my ($input) = @_;
    $input =~ s/\r\n|\r|\n/\n/g;
    my $elem = $markup->parse(\$input);
    if ($dump) {
        print Dumper($elem);
        return;
    }
    my $output = $markup->display($elem);
    print $output;
}

sub usage {
    print <<EOUSAGE;
Usage: markup [-h] [-c <content_path>] [-d] [<file>...]
EOUSAGE
    exit;
}

sub display_example {
    my ($markup, $elem) = @_;
    my $content = $elem->{content};
    my $text = $blop->escape_html($content);
    $text =~ s/^\n*//g;
    my $html = $markup->convert($content);
    if (!$elem->{hash}{p}) {
        $html =~ s{^\s*<p>|</p>\s*$}{}g;
    }
    my $output = $blop->escape_html($html);
    my $output_row = "<td title=\"Output\"><pre><code>$output</code></pre></td>";
    if ($elem->{hash}{noout}) {
        $output_row = "";
    }
    return <<EOHTML;
<table class="example">
    <tr>
        <td title="Markup"><pre><code>$text</code></pre></td>
        $output_row
        <td title="Result">$html</td>
    </tr>
</table>
EOHTML
}

