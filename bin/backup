#!/usr/bin/perl
use strict;
use warnings;
use lib "lib";
use Blop;

# mysqldump to /admin/priv/*.sql
# tarball everything into /admin/priv/backup.tar.gz

my $conf = Blop::Config::read("blop.conf");
my $blop = Blop->new;
my $dbh = $blop->dbh($conf);
my $sth = $dbh->prepare("show tables");
$sth->execute;
while (my ($table) = $sth->fetchrow_array) {
    print "Dumping $table\n";
    my @cmd = ("mysqldump", "-h", $conf->{dbhost}, "-u", $conf->{dbuser},
               "-p$conf->{dbpass}", $conf->{dbtable}, $table,
               "--skip-extended-insert");
    open my $cmd_fh, "-|", @cmd or die "$!\n";
    my $file = "admin/priv/$table.sql";
    open my $fh, ">", $file or die "Can't open $file: $!\n";
    while (my $line = <$cmd_fh>) {
        print $fh $line;
    }
    close $fh;
    close $cmd_fh;
}

print "Creating archive\n";
system "tar", "czf", "admin/priv/backup.tar.gz", ".", "--exclude", "./admin/priv/backup.tar.gz";
print "Done!\n";

